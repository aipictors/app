plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
}

def keystorePropertiesFile = rootProject.file('key.properties')
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

// 参考: https://zenn.dev/altiveinc/articles/separating-environments-in-flutter
def dartDefines = [:];
if (project.hasProperty('dart-defines')) {
    dartDefines = dartDefines + project.property('dart-defines').split(',')
        .collectEntries { entry ->
            // Flutter may encode empty values as just `KEY` (no `=`),
            // so we split into at most 2 parts and treat missing value as empty.
            def pair = new String(entry.decodeBase64(), 'UTF-8').split('=', 2)
            def key = pair[0]
            def value = pair.length > 1 ? pair[1] : ''
            [(key): value]
        }
}

android {
    namespace "com.aipictors"
    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.aipictors.app"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        // minSdkVersion flutter.minSdkVersion
        minSdkVersion 34
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        // If appIdSuffix is provided via --dart-define(-from-file) and is empty (production),
        // we should NOT fall back to a default value.
        def appIdSuffix = dartDefines.containsKey('appIdSuffix') ? dartDefines.appIdSuffix : 'debug'
        if (appIdSuffix != null && appIdSuffix.toString().length() > 0) {
            applicationIdSuffix appIdSuffix
        }

        resValue "string", "app_name", dartDefines.appName ?: "Aipictors"
        multiDexEnabled true
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfigs {
                if (keystorePropertiesFile.exists()) {
                    release {
                        keyAlias keystoreProperties['keyAlias']
                        keyPassword keystoreProperties['keyPassword']
                        storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
                        storePassword keystoreProperties['storePassword']
                    }
                }
            }
            buildTypes {
                release {
                    signingConfig keystorePropertiesFile.exists() ? signingConfigs.release : signingConfigs.debug
                }
            }
        }
    }
}

flutter {
    source '../..'
}

def flavor = dartDefines.flavor ?: 'debug'

task setupFlavor(type: Exec) {
    commandLine 'sh', '-c', """
        if [ -f "src/${flavor}/google-services.json" ]; then
            cp "src/${flavor}/google-services.json" ./google-services.json
        fi
        if [ -d "src/${flavor}/res" ]; then
            cp -r "src/${flavor}/res/." src/main/res/
        fi
    """
}

afterEvaluate {
    tasks.named('preBuild').configure {
        dependsOn setupFlavor
    }
}
